[out:json][timeout:60];
//area(id:3600359761)->.searchArea;//Oakland County
(
area(id:3600135057);//Southfield
area(id:3600134539);//Troy
area(id:3600134545);//Madison Heights
area(id:3600134544);//Ferndale
area(id:3600134565);//Bloomfield Township
area(id:3600571941);//Southfield Township
area(id:3600572701);//Royal Oak Charter Township
area(id:3600134563);//Oak Park
area(id:3600134547);//Pleasant Ridge
area(id:3600135051);//Lathrup Village
area(id:3600134536);//Bloomfield Hills
area(id:3600134537);//Birmingham
area(id:3600134533);//Berkley
area(id:3600134548);//Huntington Woods
area(id:3600134546);//Royal Oak
area(id:3600134538);//Clawson
area(id:3600134543);//Hazel Park
)->.searchArea;

// Get all highway ways and their nodes
(way["highway"](area.searchArea);)->.highwayWays;
(.highwayWays; >>;)->.highwayNodes;

//Get ways with area=yes and their nodes
(way.highwayWays["area"="yes"];)->.areaWays;
(.areaWays; >;)->.areaNodes;

//substract those nodes
(.highwayNodes; - .areaNodes;)->.highwayNodes;

// Get all landuse=* or natural=* or landcover=* ways and their nodes 
(way["landuse"](area.searchArea);)->.landuseWays;
(way["landcover"](area.searchArea);.landuseWays;)->.landuseWays;
(way["natural"]["natural"!="cave_entrance"](area.searchArea);.landuseWays;)->.landuseWays;
(.landuseWays; >>;)->.landuseNodes;

// Intersect: nodes that are in both groups
(node.highwayNodes.landuseNodes;)->.intersectNodes;
node.intersectNodes[!"entrance"]["barrier"!="gate"];
out geom;