[out:json][timeout:60];
//area(id:3600359761)->.searchArea;//Oakland County
(
//area(id:3600134565);//Bloomfield Township
//area(id:3600134536);//Bloomfield Hills
//area(id:3600134537);//Birmingham
//area(id:3600571941);//Southfield Township
//area(id:3600135057);//Southfield
//area(id:3600135051);//Lathrup Village
//area(id:3600134539);//Troy
//area(id:3600134545);//Madison Heights
//area(id:3600134546);//Royal Oak
//area(id:3600134538);//Clawson
//area(id:3600134563);//Oak Park
//area(id:3600572701);//Royal Oak Charter Township  
//area(id:3600134547);//Pleasant Ridge
//area(id:3600134544);//Ferndale 
//area(id:3600134543);//Hazel Park
//area(id:3600134533);//Berkley
//area(id:3600134548);//Huntington Woods
)->.searchArea;

//Get all highways
way[highway](area.searchArea)->.highways;
//way[highway]({{bbox}})->.highways;

//Tiger tags
way.highways[~"tiger.*"~".*"];out geom;

//Get roads
way.highways["highway"~"(motorway|trunk|primary|secondary|tertiary|unclassified|residential)(_link)?"]->.highwayRoad;
(.highwayRoad; >>;)->.highwayRoadNodes;

//Get sidewalks
way.highways["highway"~"(footway|cycleway|path)"]->.highwayWay;
way.highwayWay[~"(footway|cycleway|path)"~"side(walk|path)"]->.highwaySidewalk;
(.highwaySidewalk; >>;)->.highwaySidewalkNodes;

//Get crossings
way.highwayWay[~"(footway|cycleway|path)"~"crossing"]->.highwayCrossing;
(.highwayCrossing; >>;)->.highwayCrossingNodes;

//Get links
way.highwayWay[~"(footway|cycleway|path)"~"link"]->.highwayLink;
(.highwayLink; >>;)->.highwayLinkNodes;

//Find roads without lane definition
(way.highwayRoad[!"lanes"][!"lane_markings"];);out geom;

//Find roads without surface
(way.highwayRoad[!"surface"];);out geom;

//Get connecting nodes of roads and sidewalks -> should not exist
(node.highwayRoadNodes.highwaySidewalkNodes;);out geom;

//Get connecting nodes of roads and crossing -> should have highway=crossing
(node.highwayRoadNodes.highwayCrossingNodes;)->.intersectCrossing;
(node.intersectCrossing["highway"!="crossing"];);out geom;

//Get connecting nodes of roads and links -> should have no highway=crossing
(node.highwayRoadNodes.highwayLinkNodes;)->.intersectLink;
(node.intersectLink["highway"="crossing"];);out geom;

//Get connecting nodes of crossings/links and sidewalks -> should have kerb=*
(node.highwayCrossingNodes;.highwayLinkNodes;)->.highwayCrossingLinkNodes;
(node.highwayCrossingLinkNodes.highwaySidewalkNodes;)->.intersectKerb;
node.intersectKerb["barrier"!="kerb"]->.intersectKerb;
node.intersectKerb["kerb"!="no"];out geom;